AC_DEFUN([AC_CHECK_LIBPERL],
[
  AC_MSG_CHECKING([for libperl])
  AC_CACHE_VAL(ac_cv_have_libperl,
  [
    ac_save_libs="$LIBS"
    LIBS=`perl -MExtUtils::Embed -e ldopts`
    ac_CPPFLAGS_save="$CPPFLAGS"
    CPPFLAGS="$CPPFLAGS `perl -MExtUtils::Embed -e ccopts`"
    ac_LDFLAGS_save="$LDFLAGS"
    LDFLAGS="$LDFLAGS $all_libraries"
    AC_TRY_LINK([
      #include <EXTERN.h>
      #include <perl.h>
      ], [static PerlInterpreter *my_perl;],
      ac_cv_have_libperl="yes",
      ac_cv_have_libperl="no"
    )
    LIBS="$ac_save_libs"
    CPPFLAGS="$ac_CPPFLAGS_save"
    LDFLAGS="$ac_LDFLAGS_save"
  ])
  AC_MSG_RESULT($ac_cv_have_libperl)
  if test "$ac_cv_have_libperl" = "yes"; then
    LIBPERL_LIBS=`perl -MExtUtils::Embed -e ldopts`
    LIBPERL_INCLUDES=`perl -MExtUtils::Embed -e ccopts`
    AC_DEFINE(HAVE_LIBPERL, 1, [Define if you have libperl libraries and header files.])
  fi
])

AC_CHECK_LIBPERL
AC_SUBST(LIBPERL_LIBS)
AC_SUBST(LIBPERL_INCLUDES)
